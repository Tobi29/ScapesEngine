/*
 * Copyright 2012-2018 Tobi29
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

buildscript {
    def kotlinVersion = "1.3.20-dev-110"
    def dokkaVersion = "0.9.17"

    repositories {
        jcenter()
        maven {
            url "https://dl.bintray.com/kotlin/kotlin-dev"
        }
    }

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlinVersion"
        classpath "org.jetbrains.dokka:dokka-gradle-plugin:$dokkaVersion"
    }
}

allprojects {
    group = "com.github.Tobi29.ScapesEngine"

    repositories {
        jcenter()
        maven {
            url "https://dl.bintray.com/kotlin/kotlin-dev"
        }
        maven {
            url "https://dl.bintray.com/spekframework/spek-dev"
        }
        maven {
            url "https://maven-eclipse.github.io/maven"
        }
        maven {
            url "https://jitpack.io"
        }
    }

    afterEvaluate {
        def kotlin = extensions.findByName("kotlin")
        if (kotlin != null) {
            kotlin.sourceSets.each { sourceSet ->
                sourceSet.languageSettings {
                    languageVersion = "1.2"
                    apiVersion = "1.2"
                }
            }

            // Clean, nice, future proof code:
            def lwjgl3Modules = ["gl-backend", "gles-backend", "glfw-backend",
                    "lwjgl3-backend", "stb-truetype-font"]

            kotlin.targets.each { target ->
                def publication = publishing.publications.getByName(target.name)
                publication.pom.withXml {
                    def root = asNode()
                    def dependencies = root.get("dependencies")[0]
                    if (dependencies == null) {
                        dependencies = root.appendNode("dependencies")
                    }
                    publication.pom.runtimeDependencies
                            .each { mavenDependency ->
                        def groupId = mavenDependency.groupId
                        if (groupId != group) {
                            return
                        }
                        def artifactId = mavenDependency.artifactId
                        def suffix = target.name
                        if (suffix == "jvm-lwjgl3") {
                            suffix = "jvm"
                        }
                        if (suffix == "jvm" &&
                                lwjgl3Modules.contains(artifactId)) {
                            suffix = "jvm-lwjgl3"
                        }
                        def dependency = dependencies.
                                appendNode("dependency")
                        dependency.appendNode("groupId", groupId)
                        dependency.
                                appendNode("artifactId", "$artifactId-$suffix")
                        dependency.
                                appendNode("version", mavenDependency.version)
                        dependency.appendNode("scope", "runtime")
                    }
                }
            }
        }

        tasks.withType(org.jetbrains.kotlin.gradle.dsl.KotlinJsCompile) {
            compileTask ->
                compileTask.kotlinOptions.metaInfo = true
                compileTask.kotlinOptions.sourceMap = true
                compileTask.kotlinOptions.sourceMapEmbedSources = "always"
                compileTask.kotlinOptions.moduleKind = "umd"
        }
    }
}

ext {
    kotlinVersion = "1.2.70"
    kotlin = { module -> "org.jetbrains.kotlin:$module-common:$kotlinVersion" }
    kotlinJVM = { module -> "org.jetbrains.kotlin:$module:$kotlinVersion" }
    kotlinJS = { module -> "org.jetbrains.kotlin:$module-js:$kotlinVersion" }

    kotlinxCoroutinesVersion = "0.25.3"
    kotlinxCoroutines = { module ->
        "org.jetbrains.kotlinx:$module-common:$kotlinxCoroutinesVersion"
    }
    kotlinxCoroutinesJVM = { module ->
        "org.jetbrains.kotlinx:$module:$kotlinxCoroutinesVersion"
    }
    kotlinxCoroutinesJS = { module ->
        "org.jetbrains.kotlinx:$module-js:$kotlinxCoroutinesVersion"
    }

    slf4jVersion = "1.7.25"
    slf4jJVM = { module -> "org.slf4j:$module:$slf4jVersion" }

    spekVersion = "2.0.0-alpha.1"
    spek = { module -> "org.spekframework.spek2:$module:$spekVersion" }
    spekJVM = { module -> spek("$module-jvm") }

    lwjglVersion = "3.2.0"
    lwjglJVM = { module -> "org.lwjgl:$module:$lwjglVersion" }

    webKTBindingsVersion = "0.0.5"
    webKTBindingsJS = { module ->
        "com.github.Tobi29.WebKTBindings:$module:$webKTBindingsVersion"
    }

    junitJupiterVersion = "5.2.0"
    junitJupiterJVM =
            { module -> "org.junit.jupiter:$module:$junitJupiterVersion" }

    swtVersion = "4.6"
    swtWindows32 = "org.eclipse.swt:org.eclipse.swt.win32.win32.x86:$swtVersion"

    antlrVersion = "4.7.1"
    antlrJVM = { module -> "org.antlr:$module:$antlrVersion" }

    mariaDBJavaClientVersion = "1.7.4"
    mariaDBJavaClientJVM =
            { module -> "org.mariadb.jdbc:$module:$mariaDBJavaClientVersion" }

    sqliteJDBCVersion = "3.23.1"
    sqliteJDBCJVM = { module -> "org.xerial:$module:$sqliteJDBCVersion" }

    sqljetVersion = "1.1.11"
    sqljetJVM = { module -> "org.tmatesoft.sqljet:$module:$sqljetVersion" }

    jlayerVersion = "1.0.1"
    jlayerJVM = { module -> "javazoom:$module:$jlayerVersion" }

    concentusVersion = "fdf276ed6b"
    concentusJVM = { module ->
        "com.github.lostromb.concentus:$module:$concentusVersion"
    }

    jorbisVersion = "0.0.17"
    jorbisJVM = { module -> "org.jcraft:$module:$jorbisVersion" }
}
